<?xml version = "1.0" encoding = "utf-8" ?> 

<!--********** Copyright 2016 Roku Corp.  All Rights Reserved. **********-->

<component name = "AudioListExample" extends = "Scene" >

  <script type = "text/brightscript" >
    <![CDATA[

    sub init()
      m.top.backgroundColor = "0x000000FF"
      m.top.backgroundURI = ""

      m.audiolist = m.top.findNode("audioLabelList")
      m.playingStateLabel = m.top.findNode("playingState")
      m.playingStationLabel = m.top.findNode("playingStation")
      m.songLabel = m.top.findNode("song")
      m.currentSongUri = ""

      m.audiolist.observeField("itemSelected", "setandplayaudio")

      m.audio = createObject("RoSGNode", "Audio")

      m.audio.observeField("state", "controlaudioplay")
      m.audio.observeField("state", "setaudiostatelabel")

      m.readAudioContentTask = createObject("RoSGNode", "ContentReader")
      m.readAudioContentTask.observeField("content", "showaudiolist")
      m.readAudioContentTask.contenturi = "https://gist.githubusercontent.com/jonathonwpowell/23e656ddc4a44bfed436fc44c80b1d52/raw"
      m.readAudioContentTask.control = "RUN"

      m.timer = m.top.findNode("whatsPlayingTimer")
      m.timer.control = "start"
      m.timer.ObserveField("fire","updatecurrentsong")
    end sub

    sub updatecurrentsong()
      'print "update current song starting"
      m.currentSongReader = createObject("RoSGNode", "CurrentSongReader")
      m.currentSongReader.uri = m.currentSongUri
      m.currentSongReader.songlabel = m.songLabel
      'm.currentSongReader.artistlabel = m.artistLabel
      m.currentSongReader.control = "RUN"
    end sub

    sub showaudiolist()
      m.audiolist.content = m.readAudioContentTask.content
      m.audiolist.setFocus(true)
    end sub

    sub setandplayaudio()
      audiocontent = m.audiolist.content.getChild(m.audiolist.itemSelected)
      m.playingStationLabel.text = "Station: " + audiocontent.TITLE
      print audiocontent.CURRENTSONGURL
      m.currentSongUri = audiocontent.CURRENTSONGURL
      m.audio.content = audiocontent
      m.audio.control = "stop"
      m.audio.control = "none"
      m.audio.control = "play"
      m.songLabel.text = ""
      updatecurrentsong()
    end sub

    sub controlaudioplay()
      if (m.audio.state = "finished") 
        m.audio.control = "stop"
        m.audio.control = "none"
      end if
    end sub

    sub setaudiostatelabel()
      m.playingStateLabel.text = m.audio.state
    end sub

    function onKeyEvent(key as String,press as Boolean) as Boolean
      if press then
        ' print "key is: " + key
        if key = "back" OR key = "play"
          if (m.audio.state = "playing")
            m.audio.control = "stop"

            return true
          end if
        end if
      end if

      return false
    end function

    ]]>
  </script>

  <children>

    <Group >


      <LabelList 
        id = "audioLabelList" 
        translation = "[ 100, 240 ]" 
        itemSize = "[ 450, 60 ]" />
        
      <Label
        id= "playingState"
        height= "40"
        width= "200"
        text= ""
        translation = "[ 100, 570 ]"
        font = "font:LargeBoldSystemFont" 
      />

      <Label
        id= "playingStation"
        height= "80"
        width= "540"
        text= ""
        wrap= "true"
        translation = "[ 100, 620 ]"
        font = "font:LargeBoldSystemFont"  
      />

      <Label
      id= "song"
      height= "250"
      width= "550"
      wrap= "true"
      text= ""
      translation = "[ 650, 250 ]"
      font = "font:LargeSystemFont" 
      />

      <Label
      id= "wkncinfo"
      height= "400"
      width= "550"
      wrap= "true"
      text= "WKNC is student-run non-commercial radio from NC State University. Visit wknc.org for playlists, schedules, and more! Catch us over air at 88.1 around Raleigh, NC."
      translation = "[ 650, 520 ]"
      font = "font:MediumSystemFont" 
      />

      <Poster 
        uri = "pkg:/images/wknc_inline.png" 
        translation = "[ 400, 20 ]" 
        width = "480"
        height = "144" />

      <Timer
        id="whatsPlayingTimer"
        repeat="true"
        duration="15"/>

    </Group>

  </children>

</component>
